option solver cplex;
option solution_round 6;

model withPlanning/cut.mod;
data withPlanning/cut.dat;

problem Cutting_Opt: Cut, Number, Fill;
   option relax_integrality 1;
   option presolve 0;

problem Pattern_Gen: Use, ReducedCost, WidthLimit;
   option relax_integrality 0;
   option presolve 1;

#let relaxCost := 100; #uncomment to solve without relax
let nPAT := 0;
#setting up one pattern for every width
for {i in ORDERS} 
{			
	let nPAT := nPAT + 1;
	let wfep[i,nPAT] := floor (rawBarWidth/widths[i]);
	let {i2 in ORDERS: i2 <> i} wfep[i2,nPAT] := 0;
};

repeat
{
	solve Cutting_Opt;
	#new price is a cost in raw bars associated with incrementing orders by 1 for every width
	let {i in ORDERS} price[i] := Fill[i].dual;
	solve Pattern_Gen;
	if ReducedCost > 0.00001 then 
	{
		let nPAT := nPAT + 1;
		let {i in ORDERS} wfep[i,nPAT] := Use[i];
	}
	else
		break;
};

display wfep;
display Cut;

option Cutting_Opt.relax_integrality 0;
option Cutting_Opt.presolve 10;
solve Cutting_Opt;

display Cut;
display _total_solve_elapsed_time;